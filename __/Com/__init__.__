class Com

 var public COM_OPTS = struct
  {
  bg_jobs = 1,
  chdir   = 1,
  search  = 1,
  rehash  = 1,
  eval    = 1,
  ved     = 1,
  };

  let COMMANDS_FOR_PAGER = NULL;

  subclass Fork from Com

  def pre_com ()
    ICOM++;
    ERR_VED.st_.st_size = fstat (This.is.std.err.fd).st_size;
  end

  def pre_header (argv)
    IARG++;
    if (This.is.shell)
      IO.tostdout (strjoin (argv, " "));
    else
      __toscratch  (strjoin (argv, " ") + "\n");
  end

  def post_header ()
    if (This.is.shell)
      IO.tostdout (sprintf ("[%d](%s)[%d]$ ", IARG, getcwd, EXITSTATUS); n);
    else
      __toscratch  (sprintf ("[%d](%s)[%d]$ ", IARG, getcwd, EXITSTATUS));
  end

  def post_exec (header)
    if (qualifier_exists ("draw") && qualifier ("draw") == 0)
      return;

    if (header)
      self.post_header ();

    if (VED_WIND_NEEDS_DRAW)
      {
      Ved.draw_wind ();
      VED_WIND_NEEDS_DRAW = 0;
      }
    else
      __draw_buf (Ved.get_cur_buf ());
  end

  def pre_exec (argv, header, issu, env)
    self.pre_com ();

    @header = strlen (argv[0]) > 1 && 0 == qualifier_exists ("no_header");
    @issu = qualifier ("issu");
    @env = [Env.defenv (), "PPID=" + string (Env->PID), "CLNT_FIFO=" + RDFIFO,
      "SRV_FIFO=" + WRFIFO];

    variable p = Proc.init (@issu, 0, 0);

    p.issu = 0 == @issu;

    if (@header)
      self.pre_header (argv);

    if ('!' == argv[0][0])
      argv[0] = substr (argv[0], 2, -1);

    argv = [Sys->SLSH_BIN, Env->STD_LIB_PATH + "/proc/loadcommand.slc", argv];

    if (@issu)
      {
      p.stdin.in = qualifier ("passwd");
      if (NULL == p.stdin.in)
        {
        EXITSTATUS = 1;

        if (@header)
          self.post_header ();

        return NULL;
        }

      argv = [Sys->SUDO_BIN, "-S", "-E", "-p", "", argv];
      }

    argv, p;
  end

  def sig_send (sig, pid, passwd)
    variable p = Proc.init (1, 0, 0);
    p.stdin.in = passwd;

    () = p.execv ([Sys->SUDO_BIN, "-S", "-E", "-p", "", Sys->SLSH_BIN,
      Env->STD_LIB_PATH + "/proc/sendsignalassu.slc", sig, pid], NULL);
  end

  def get_bgstatus (pid)
    variable pidfile = BGDIR + "/" + pid + ".WAIT";
    variable force = qualifier_exists ("force");
    variable isnotsu = BGPIDS[pid].issu;

    if (-1 == access (pidfile, F_OK))
      ifnot (force)
        return;
      else
        pidfile = BGDIR + "/" + pid + ".RUNNING";

    if (0 == isnotsu && Env->UID)
      {
      variable passwd = Os.__getpasswd ();
      if (NULL == passwd)
        return;

      self.sig_send (string (SIGKILL), pid, passwd);
      }
    else
      if (-1 == kill (atoi (pid), SIGALRM))
        {
        IO.tostderr (pid + ": " + errno_string (errno));
        return;
        }

    if (isnotsu || (isnotsu == 0 == Env->UID))
      {
      variable rdfd = open (RDFIFO, O_RDONLY);
      variable buf = Sock.get_str (rdfd);

      buf = strtrim_end (buf);

      ifnot ("exit" == buf)
        return;
      }

    variable status = waitpid (atoi (pid), 0);

    variable out = File.read (STDOUTFDBG;offset = OUTBG_VED.st_.st_size);

    if (strbytelen (out))
      out = strjoin (strtok (out, "\n"), "\n");

    ifnot (NULL == out)
      if (This.is.shell)
        IO.tostdout ("\n" + pid + ": " + strjoin (BGPIDS[pid].argv, " ") + "\n" +  out);
      else
        __toscratch  ("\n" + pid + ": " + strjoin (BGPIDS[pid].argv, " ") + "\n" +  out);

    ifnot (force)
      if (This.is.shell)
        IO.tostdout (pid + ": exit status " + string (status.exit_status));
      else
        __toscratch  (pid + ": exit status " + string (status.exit_status) + "\n");

    BGPIDS[pid].atexit ();

    assoc_delete_key (BGPIDS, pid);

    () = remove (pidfile);
  end

  def get_bgjobs ()
    variable pids = assoc_get_keys (BGPIDS);

    ifnot (length (pids))
      return;

    variable i;

    _for i (0, length (pids) - 1)
      self.get_bgstatus (pids[i]);
  end

  def kill_bg_job (argv)
    self.pre_header (argv);

    if (1 == length (argv))
      {
      self.post_header ();
      __draw_buf (Ved.get_cur_buf ());
      return;
      }

    variable pid = argv[1];

    ifnot (assoc_key_exists (BGPIDS, pid))
      {
      self.post_header ();
      __draw_buf (Ved.get_cur_buf ());
      return;
      }

    self.get_bgstatus (pid;force);

    if (This.is.shell)
      IO.tostdout (pid + ": killed");
    else
      Smg.send_msg_dr (pid + ": killed", 0, PROMPTROW, 1);

    self.post_header ();
    __draw_buf (Ved.get_cur_buf ());
  end

  def list_bg_jobs (argv)
    self.pre_header (argv);

    variable ar = String_Type[0];
    variable i;
    variable pids = assoc_get_keys (BGPIDS);

    ifnot (length (pids))
      {
      self.post_header ();
      __draw_buf (Ved.get_cur_buf ());
      return;
      }

    _for i (0, length (pids) - 1)
      Array.append (&ar, pids[i] + ": " + strjoin (BGPIDS[pids[i]].argv, " ") + "\n");

    IO.tostdout (ar);

    self.post_header ();

    __draw_buf (Ved.get_cur_buf ());
  end

  def pre_builtin (argv)
    EXITSTATUS = 0;
    self.pre_com ();
    self.pre_header (argv);
  end

  def post_builtin ()
    variable err = File.read (This.is.std.err.fd;
      offset = ERR_VED.st_.st_size);

    ifnot (NULL == err)
      if (This.is.shell)
        IO.tostdout (strtrim_end (err));
      else
        __toscratch  (strtrim_end (err) + "\n");

    self.post_header ();

    __draw_buf (Ved.get_cur_buf ());
  end

  var licom = 0;
  var icom  = 0;
  var redirexists = NULL;

  def parse_redir (lastarg, file, flags)
    variable index = 0;
    variable chr = lastarg[index];
    variable redir = chr == '>';

    ifnot (redir)
      return 0;

    variable lfile;
    variable lflags = ">";
    variable len = strlen (lastarg);

    index++;

    if (len == index)
      return 0;

    chr = lastarg[index];

    if (chr == '>' || chr == '|')
      {
      lflags += char (chr);
      index++;

      if (len == index)
        {
        IO.tostderr ("There is no file to redirect output");
        return -1;
        }
      }

    chr = lastarg[index];

    if (chr == '|')
      {
      lflags += char (chr);
      index++;

      if (len == index)
        {
        IO.tostderr ("There is no file to redirect output");
        return -1;
        }
      }

    lfile = substr (lastarg, index + 1, -1);

    ifnot (access (lfile, F_OK))
      {
      ifnot ('|' == lflags[-1])
        if (NULL == redirexists || (NULL != redirexists && licom + 1 != icom))
          {
          if (">" == lflags)
            {
            licom = icom;
            redirexists = 1;
            IO.tostderr (lfile + ": file exists, use >|");
            return -1;
            }
          }
        else
          if (">" == lflags)
            {
            redirexists = NULL;
            licom = 0;
            lflags = ">|";
            }

      if (-1 == access (lfile, W_OK))
        {
        IO.tostderr (lfile + ": is not writable");
        return -1;
        }

      ifnot (File.is_reg (lfile))
        {
        IO.tostderr (lfile + ": is not a regular file");
        return -1;
        }
      }

    @flags = lflags;
    @file = lfile;
    1;
  end

  def parse_argv (argv, isbg)
    variable flags = ">>|";
    variable file = @isbg ? STDOUTBG : This.is.shell ? Ved.get_cur_buf ()._abspath : SCRATCH;
    variable lfile = file;

    variable retval = self.parse_redir (argv[-1], &file, &flags);

    if (lfile == file && file == SCRATCH)
      if (NULL == This.is.shell || 0 == This.is.shell)
        {
        flags = ">|";
        @isbg = 0;
        }

    file, flags, retval;
  end

  def execute (argv)
    if ('~' == argv[0][0])
      argv[0] = substr (argv[0], 2, -1);

    variable isbg = 0;
    if (argv[-1] == "&")
      {
      isbg = 1;
      argv = argv[[:-2]];
      }

    if (argv[-1][-1] == '&')
      {
      isbg = 1;
      argv[-1] = substr (argv[-1], 1, strlen (argv[-1]) - 1);
      }

    variable header, issu, env, stdoutfile, stdoutflags;

    variable p = self.pre_exec (argv, &header, &issu, &env;;__qualifiers ());

    if (NULL == p)
      return;

    argv = ();

    variable isscratch = Opt.Arg.exists ("--pager", &argv;del_arg);

    ifnot (NULL == isscratch)
      {
      isbg = 0;
      stdoutfile = SCRATCH;
      stdoutflags = ">|";
      }
    else
      {
      variable file, flags, retval;
      (file, flags, retval) = self.parse_argv (argv, &isbg);

      if (-1 == retval)
        {
        variable err = File.read (This.is.std.err.fd;offset = ERR_VED.st_.st_size);

        if (This.is.shell)
          IO.tostdout (err);
        else
          __toscratch  (err + "\n");

        ERR_VED.st_.st_size += strbytelen (err) + 1;
        EXITSTATUS = 1;
        self.post_exec (header);
        return;
        }

      if (1 == retval) % unused condition; with current code, we never get that retval
        {
        argv[-1] = NULL;
        argv = argv[wherenot (_isnull (argv))];
        }

      stdoutfile = file;
      stdoutflags = flags;
      }

    if (NULL == isscratch &&
    %%% CARE FOR CHANGES argv-index
      (any (COMMANDS_FOR_PAGER == argv[2]) &&
        NULL == Opt.Arg.exists ("--buildcache", &argv)))
      {
      isbg = 0;
      stdoutfile = SCRATCH;
      stdoutflags = ">|";
      isscratch = 1;
      }

    env = [env, "stdoutfile=" + stdoutfile, "stdoutflags=" + stdoutflags,
     "stderrfile=" + This.is.std.err.fn, "stderrflags=>>|"];

    ifnot (isbg)
      if (This.has.sigint)
        {
        variable handl;
        signal (SIGINT, SIG_IGN, &handl);
        self.Fork.tofg (p, argv, env);
        signal (SIGINT, handl);
        }
      else
        self.Fork.tofg (p, argv, env);
    else
      {
      ifnot (COM_OPTS.bg_jobs)
        return;

      self.Fork.tobg (p, argv, env);
      isscratch = NULL;
      }

    if (NULL != isscratch || 0 == This.is.shell)
      if (EXITSTATUS)
        __messages;
      else
        if (0 < lseek (SCRATCH_VED._fd, 0, SEEK_END))
          App.scratch (Ved.get_cur_buf ());

    ifnot (isbg)
      self.get_bgjobs ();

    % (ugly) hack to fix the err messages from sudo to mess the screen
    % since we don't open the stderr stream in the process
    if (issu)
      Smg.clear_and_redraw ();

    self.post_exec (header;;__qualifiers ());
  end
end
