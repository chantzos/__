subclass Run
  __init__
    init
      srv
      client
      child
    as
      client
      child
    app
  end

  def init_srv ()
  end

  def init_client ()
  end

  def init_child ()
  end

  def as_client (argv, srvf, clnf)
    variable env = [Env.defenv (),
      __get_qualifier_as (AString_Type, "env", qualifier ("env"), String_Type[0]),
      "SESSION=1",
      "SESSION_WRFIFO=" + srvf,
      "SESSION_RDFIFO=" + clnf,
      ];

    self.app (argv, env;;__qualifiers);
  end

  def as_child (argv)
    ifnot (NULL == This.isachild)
      return;

    if (NULL == App->PARENT_FIFO)
      {
      App->PARENT_FIFO = This.tmpdir + "/PARENT.fifo";

      if (-1 == mkfifo (App->PARENT_FIFO, 0755))
        {
        App->PARENT_FIFO = NULL;
        throw ClassError, "Run.as.child::cannot crate Parent's fifo, " +
          errno_string (errno);
        }
      }

    ifnot (length (argv))
      return;

    variable child = argv[0];

    variable chfifo = This.tmpdir + "/__" + child + "_child_" +
      string (_time)[[5:]] + ".fifo";

    if (-1 == mkfifo (chfifo, 0755))
      throw ClassError, "Run.as.child::cannot crate Child's fifo, " +
        errno_string (errno);

    variable env = [Env.defenv (),
      __get_qualifier_as (AString_Type, "env", qualifier ("env"), String_Type[0]),
      "PARENT_WRFIFO=" + App->PARENT_FIFO,
      "PARENT_RDFIFO=" + chfifo,
      "ISACHILD=1"];

    variable chpid = self.app (argv, env;bg);

    if (NULL == chpid)
      {
      () = remove (chfifo);
      return;
      }

    App->PARENT_FIFO_FD = open (App->PARENT_FIFO, O_RDONLY);
    variable chfifo_fd = open (chfifo, O_WRONLY);

    App->CHILDREN[string (chpid)] = @App_Type;

    variable s = App->CHILDREN[string (chpid)];
    s.fifo  = chfifo;
    s.fd    = chfifo_fd;
    s.pid   = chpid;
    s.state = Api->CONNECTED;
    s.name  = child;
    s.argv = argv;

    variable retval = Sock.get_int (App->PARENT_FIFO_FD);

    if (Api->GO_ATEXIT == retval)
      {
      () = waitpid (s.pid, 0);
      () = close (s.fd);
      () = remove (s.fifo);
      assoc_delete_key (App->CHILDREN, string (chpid));
      }
    else if (Api->GO_IDLED == retval)
      s.state |= Api->IDLED;
    else
      throw ClassError, "Run.as.child::unexpected retval from pipe";

    Api.restore_screen ();

    ifnot (qualifier_exists ("goto_rline"))
      return;

    variable rl = qualifier ("rline", Ved.get_cur_rline ());
    Rline.set (rl);
    Rline.prompt (rl, rl._lin, rl._col);
  end

  def app (argv, env)
    APP_ERR = 0;

    if (strncmp (argv[0], "__", 2))
      argv[0] = "__" + argv[0];

    argv[0] = Env->BIN_PATH + "/" + argv[0];

    if (-1 == access (argv[0], F_OK|X_OK))
      {
      IO.tostderr (argv[0], "couldn't been executed,", errno_string (errno));
      APP_ERR = 1;
      return NULL;
      }

    variable issu = qualifier ("issu");
    variable passwd = qualifier ("passwd");

    variable p = Proc.init (issu, 0, 0);

    if (issu)
      {
      if (NULL == passwd)
        {
        variable isgoingtoreset = 0;
        ifnot (This.isscreenactive)
          {
          Api.restore_screen ();
          isgoingtoreset = 1;
          }

          passwd = Os.__getpasswd ();

          if (isgoingtoreset)
            Api.reset_screen ();

          if (NULL == passwd)
            {
            APP_ERR = 1;
            return NULL;
            }
          }

      p.stdin.in = passwd;

      argv = [Sys->SUDO_BIN, "-S", "-E", "-p", "", argv];
      }

    Api.reset_screen ();

    variable status;
    variable bg = qualifier_exists ("bg") ? 1 : NULL;

    ifnot (NULL == env)
      status = p.execve (argv, env, bg);
    else
      status = p.execv (argv, bg);

    if (NULL == bg)
      Api.restore_screen ();

    status;
  end
end
