subclass Git
  __init__
    init
    status
    log
    diff
    add
    branches
    branchnew
    branchdelete
    branchchange
    commit
    commitall
    pull
    diffrevision
    applypatch
    merge
    generic
    get_upstream_url
    push
    clone
  end

  let EXEC = Sys.which ("git");

  var funcs = Assoc.new (Array_Type,
    [
    "init", "status", "log", "diff", "add", "branchnew",
    "branchdelete", "branchchange", "commit", "commitall", "pull",
    "diffrevision", "applypatch", "merge",
    "generic", "get_upstream_url", "push", "clone"
    ],
    {
    [EXEC, "init"],
    [EXEC, "status", "-s"],
    [EXEC, "log", "--pretty=format:commit: %h%ncommiter: %cn %cD%n%n  %B"],
    [EXEC, "diff"],
    [EXEC, "add", "-v"],
    [EXEC, "branch"],
    [EXEC, "branch", "-d"],
    [EXEC, "checkout"],
    [EXEC, "commit", "-v", "--message="],
    [EXEC, "commit", "-a", "-v", "--message="],
    [EXEC, "pull"],
    [EXEC, "log", "--no-walk", "-p"],
    [EXEC, "apply", "-p1", "--verbose"],
    [EXEC, "merge"],
    [EXEC],
    [EXEC, "config", "-l"],
    [EXEC, "push", "--verbose", "--repo"],
    [EXEC, "clone"],
    });

  def __exec__ (argv, status) proc
    variable p = Proc.init (0, 1, 1);
    variable file = qualifier ("redir_to_file");

    ifnot (NULL == file)
      {
      p.stdout.file = file;
      variable flags = qualifier ("flags");

      ifnot (NULL == flags)
        p.stdout.wr_flags = flags;
      }

    @status = p.execv (argv, NULL);
    p;
  end

  def __fmt_output__ (buf) proc
    strtok (buf, "\n");
  end

  def __print__ (p, status) proc
    if (status.exit_status)
      IO.tostderr (__fmt_output__ (p.stderr.out);;__qualifiers);
    else
      if (NULL == p.stdout.file)
        IO.tostdout (__fmt_output__ (p.stdout.out);;__qualifiers);

    status.exit_status;
  end

  def generic (?)
    variable args = __pop_list (_NARGS - 1);
    pop ();

    args = list_to_array (args, String_Type);

    variable
      status,
      argv = [@funcs["generic"], args],
      p = __exec__ (argv, &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def status ()
    variable
      status,
      p = __exec__ (funcs["status"], &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def pull ()
    variable
      status,
      p = __exec__ (funcs["pull"], &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def diff (?)
    variable
      status,
      args = __pop_list (_NARGS - 1),
      argv = [@funcs["diff"], list_to_array (args, String_Type)],
      p    = __exec__ (argv, &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def add (?)
    variable
      status,
      files = __pop_list (_NARGS - 1),
      argv  = [@funcs["add"], list_to_array (files, String_Type)],
      p     = __exec__ (argv, &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def get_upstream_url ()
    variable
      status,
      p = __exec__ (funcs["get_upstream_url"], &status);

    if (NULL == p.stdout.out || 0 == strlen (p.stdout.out))
      {
      IO.tostderr ("cannot read config");
      return NULL;
      }

    variable url = __fmt_output__ (p.stdout.out);

    url = strtrim_end (url);
    url = url[wherenot (strncmp (url, "remote.origin.url", strlen ("remote.origin.url")))];
    ifnot (length (url))
      {
      IO.tostderr ("I cant get upstream's url");
      return NULL;
      }

    url = strchop (url[0], '=', 0);
    ifnot (length (url) - 1)
      {
      IO.tostderr ("I cant get upstream's url");
      return NULL;
      }

    url[1];
  end

  def clone (url, as)
    variable
      status,
      argv = [@funcs["clone"], url, as],
      p = __exec__ (argv, &status;;__qualifiers);

   __print__ (p, status;;__qualifiers);
  end

  def push (url)
    variable
      status,
      argv = [@funcs["push"], url],
      p = __exec__ (argv, &status;;__qualifiers);

   __print__ (p, status;;__qualifiers);
  end

  def diffrevision (revision)
    if ('~' == revision[0])
      ifnot (NULL == String.to_int (revision[[1:]]))
        revision = sprintf ("%s%s", qualifier ("branch", "master"), revision);

    variable
      status,
      argv = [funcs["diffrevision"], revision],
      p = __exec__ (argv, &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def log (?)
    variable
      status,
      argv = @funcs["log"];

    variable args = __pop_list (_NARGS - 1);
    pop ();

    args = list_to_array (args, String_Type);

    variable p = __exec__ ([argv, args], &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def applypatch (patch)
    variable
      status,
      p = __exec__ ([funcs["applypatch"], patch], &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def merge (branch)
    variable
      status,
      p = __exec__ ([funcs["merge"], branch], &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def init ()
    variable
      status,
      p = __exec__ (funcs["init"], &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def branches ()
    variable p = Proc.init (0, 1, 1);
    variable status = p.execv ([EXEC, "branch"], NULL);

    if (status.exit_status)
      {
      IO.tostderr (__fmt_output__ (p.stderr.out);;__qualifiers);
      return NULL;
      }

    variable b = struct {branches, cur};
    variable ar = strtrim_end (strchop (p.stdout.out, '\n', 0));
    ar = ar[where (strlen (ar))];

    ifnot (length (ar))
      b.branches = String_Type[0];
    else
      {
      b.branches = strtrim_beg (strtrim_beg (ar, "*"));
      b.cur = substr (ar[wherenot (strncmp (ar, "* ", 2))][0], 3, -1);
      }

    b;
  end

  def branchnew (branch)
    variable
      status,
      argv = [funcs["branchnew"], branch],
      p = __exec__ (argv, &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def branchdelete (branch)
    variable
      status,
      argv = [funcs["branchdelete"], branch],
      p = __exec__ (argv, &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def branchchange (branch)
    variable
      status,
      argv = [funcs["branchchange"], branch],
      p = __exec__ (argv, &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def commit (?)
    variable
      files = __pop_list (_NARGS - 1);

    pop ();

    if (0 >= length (files) - 1)
      {
      IO.tostderr ("no file[s] provided");
      return 1;
      }

    variable
      msg = files[-1],
      status,
      author = qualifier ("author"),
      argv = @funcs["commit"];

    argv[-1] += msg;

    ifnot (NULL == author)
      argv = [argv, "--author=" + author];

    argv = [argv, list_to_array (files[[:-2]], String_Type)];

    variable p = __exec__ (argv, &status;;__qualifiers);

    __print__ (p, status;;__qualifiers);
  end

  def commitall (msg)
    variable
      status,
      author = qualifier ("author"),
      argv = @funcs["commitall"];

    argv[-1] += msg;

    ifnot (NULL == author)
      argv = [argv, "--author=" + author];

    variable p = __exec__ (argv, &status;;__qualifiers);

   __print__ (p, status;;__qualifiers);
  end
end
